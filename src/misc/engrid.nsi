; !define FASTBUILD ; uncomment for debugging this script

;Accepted name defines:
;  USE_mingw32
;  USE_VisualCppExpress2008
;  USE_VisualCppExpress2008_x64

;Accepted value defines:
; OUTPUTFOLDER="Folder path"
; BUILDVERSION="win32"
; PRODUCT_VERSION="1.5"
; PRODUCT_VERSIONx4=1.5.0.0

; Script generated by the HM NIS Edit Script Wizard.
; TODO: The MinGW version is unfinished...
!ifdef USE_mingw32
  !define MINGWBINDIR "c:\qt\2009.03\mingw\bin"
  !define QTBINDIR "c:\qt\2009.03\qt\bin"
  !define VTKBINDIR "P:\VTK\bin"
!endif
!ifdef USE_VisualCppExpress2008
  !define QTBINDIR "${SRC_ROOT}\third_party\Qt\bin"
  !define VTKBINDIR "${SRC_ROOT}\third_party\VTK\bin"
  !define VTKLIBDIR "${SRC_ROOT}\third_party\VTK\lib"
  !define BRLCADDIR "${SRC_ROOT}\third_party\BRLCAD"
  !define BRLCADBINDIR "${SRC_ROOT}\third_party\BRLCAD\bin"
!endif

!ifdef USE_VisualCppExpress2008_x64
  !define QTBINDIR "${SRC_ROOT}\third_party64\Qt\bin"
  !define VTKBINDIR "${SRC_ROOT}\third_party64\VTK\bin"
  !define VTKLIBDIR "${SRC_ROOT}\third_party64\VTK\lib"
  !define BRLCADDIR "${SRC_ROOT}\third_party64\BRLCAD"
  !define BRLCADBINDIR "${SRC_ROOT}\third_party64\BRLCAD\bin"
  !ifndef USE_VisualCppExpress2008
  !define USE_VisualCppExpress2008 ; useful for redundant operations!
  !endif
  !define WIN64
!endif

; FIXME: These two are to be dropped?
!define QWTLIBDIR "c:\Libraries\qwt-5.3.0-svn\lib"
!define QWTPLOT3DLIBDIR "c:\Libraries\qwtplot3d\lib"

;Default values:
!ifndef OUTPUTFOLDER
!define OUTPUTFOLDER "${SRC_ROOT}\windows"
!endif

!ifndef BUILDVERSION
!define BUILDVERSION "win32"
!endif

!ifndef PRODUCT_VERSION
!define PRODUCT_VERSION "1.5alpha"
!endif

!ifndef PRODUCT_VERSIONx4
!define PRODUCT_VERSIONx4 1.5.0.0
!endif

; Set up general options
; SetCompressor lzma
!ifndef FASTBUILD
  SetCompressor /SOLID lzma ; saves about 1.2MB
  SetCompressorDictSize 128
!endif


; set-up UAC
RequestExecutionLevel user
!addincludedir NSIS
!addplugindir NSIS
!include UAC.nsh

!ifdef WIN64
!include x64.nsh
!endif

; Load headers for file functions
!include "FileFunc.nsh"


; HM NIS Edit Wizard helper defines
!define SRC_ROOT "..\"
!define PRODUCT_NAME "enGrid"
; PRODUCT_VERSION defined on the header part of this file
!define PRODUCT_PUBLISHER "enGits GmbH"
!define PRODUCT_WEB_SITE "http://www.engits.eu"
!define WIKI_WEB_SITE "https://github.com/enGits/engrid/wiki"
!define PRODUCT_DIR_REGKEY "Software\enGits\${PRODUCT_NAME}"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define PRODUCT_STARTMENU_REGVAL "NSIS:StartMenuDir"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\orange-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\orange-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "..\licence_exe.txt"

; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Start menu page
var ICONS_GROUP
var DESKTOP_ICON
!define MUI_STARTMENUPAGE_NODISABLE
!define MUI_STARTMENUPAGE_DEFAULTFOLDER "$ICONS_GROUP"
!define MUI_STARTMENUPAGE_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "${PRODUCT_STARTMENU_REGVAL}"
!insertmacro MUI_PAGE_STARTMENU Application $ICONS_GROUP
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES

; Finish page
; Overriding the execution of enGrid to be done as normal user.
Function ExecAppFile
  Exec '$INSTDIR\engrid.exe'
FunctionEnd

Function AppRunAs
  !insertmacro UAC.CallFunctionAsUser ExecAppFile
FunctionEnd

!define MUI_FINISHPAGE_RUN
!define MUI_FINISHPAGE_RUN_FUNCTION AppRunAs

Function finishpageaction
  SetShellVarContext all
  CreateShortCut "$DESKTOP\enGrid.lnk" "$INSTDIR\engrid.exe" " " "$INSTDIR\engrid.exe"
  StrCpy $DESKTOP_ICON "1"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_DIR_REGKEY}" "DesktopIcon" "$DESKTOP_ICON"
FunctionEnd

!define MUI_FINISHPAGE_SHOWREADME ""
!define MUI_FINISHPAGE_SHOWREADME_NOTCHECKED
!define MUI_FINISHPAGE_SHOWREADME_TEXT "Create Desktop Shortcut"
!define MUI_FINISHPAGE_SHOWREADME_FUNCTION finishpageaction

!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "${OUTPUTFOLDER}\${PRODUCT_NAME}_${PRODUCT_VERSION}_setup_${BUILDVERSION}.exe"
!ifdef WIN64
  InstallDir "$PROGRAMFILES64\enGits\enGrid"
!else
  InstallDir "$PROGRAMFILES\enGits\enGrid"
!endif
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Caption "${PRODUCT_NAME} ${PRODUCT_VERSION}"
VIProductVersion ${PRODUCT_VERSIONx4}
VIAddVersionKey ProductName "${PRODUCT_NAME}"
VIAddVersionKey Comments "Installer for ${PRODUCT_NAME} ${PRODUCT_VERSION}, build version ${BUILDVERSION}."
VIAddVersionKey CompanyName "${PRODUCT_PUBLISHER}"
VIAddVersionKey LegalCopyright "${PRODUCT_WEB_SITE}"
VIAddVersionKey FileDescription "${PRODUCT_NAME} ${PRODUCT_VERSION} installer."
VIAddVersionKey FileVersion ${PRODUCT_VERSIONx4}
VIAddVersionKey ProductVersion  ${PRODUCT_VERSIONx4}
VIAddVersionKey InternalName "${PRODUCT_NAME} ${PRODUCT_VERSION} installer."
VIAddVersionKey LegalTrademarks "enGrid is a Trademark of enGits, GmbH."
VIAddVersionKey OriginalFilename "${PRODUCT_NAME}_${PRODUCT_VERSION}_setup_${BUILDVERSION}.exe"

Function .onInit

!ifdef WIN64
  SetRegView 64
  ${DisableX64FSRedirection}

  ${If} ${RunningX64}
    ${EnableX64FSRedirection}
  ${else}
    MessageBox MB_OK "Sorry this application runs only on x64 machines"
    Abort
  ${EndIf}
!endif

  ;Extract InstallOptions files
  ;$PLUGINSDIR will automatically be removed when the installer closes

  InitPluginsDir

  ; Initialize global variables
!ifdef WIN64
  StrCpy $ICONS_GROUP "enGits\${PRODUCT_NAME} (x64)"
  StrCpy $INSTDIR "$PROGRAMFILES64\enGits\enGrid"
!else
  StrCpy $ICONS_GROUP "enGits\${PRODUCT_NAME}"
!endif
  StrCpy $DESKTOP_ICON "0"

  ; Force installation as super-user
  UAC_Elevate:
    UAC::RunElevated
    StrCmp 1223 $0 UAC_ElevationAborted
    StrCmp 0 $0 0 UAC_Err
    StrCmp 1 $1 0 UAC_Success
    Quit

  UAC_Err:
    MessageBox mb_iconstop "Unable to elevate, error $0"
    Abort

  UAC_ElevationAborted:
    MessageBox mb_iconstop "This installer requires admin access, aborting!"
    Abort

  UAC_Success:
    StrCmp 1 $3 +4
    StrCmp 3 $1 0 UAC_ElevationAborted
    MessageBox mb_iconstop "This installer requires admin access, try again"
    goto UAC_Elevate

  ; if the application is already installed, then ask you one wants to uninstall it
  ReadRegStr $R0  ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString"
  StrCmp $R0 "" done

  ReadRegStr $R1 ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion"

  MessageBox MB_YESNO|MB_ICONEXCLAMATION "The enGrid version $R1 is already installed. $\nDo you want to remove \
                                          the previous version before installing $(^Name)?" IDNO done

  ;Run the uninstaller
  ;uninst:
    ClearErrors
    ExecWait '$R0 _?=$INSTDIR' ;Do not copy the uninstaller to a temp file

  done:

FunctionEnd

Function .OnInstFailed
    ${RefreshShellIcons}
    UAC::Unload
FunctionEnd

!define SHCNE_ASSOCCHANGED 0x08000000
!define SHCNF_IDLIST 0

Function .OnInstSuccess
    ${RefreshShellIcons}
    UAC::Unload
FunctionEnd

Section "MainSection" SEC01
  InstallOptions::dialog "$PLUGINSDIR\test.ini"

  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
!ifdef USE_mingw32
  File "${SRC_ROOT}\release\engrid.exe"
  File "${SRC_ROOT}\release\libengrid.dll"
  File "${SRC_ROOT}\release\nglib.dll"
!else ifdef USE_VisualCppExpress2008_x64
  File "${SRC_ROOT}\windows\VisualCppExpress2008\x64\Release\engrid.exe"
  File "${SRC_ROOT}\windows\VisualCppExpress2008\x64\Release\libengrid.dll"
  File "${SRC_ROOT}\windows\VisualCppExpress2008\x64\Release\nglib.dll"
!else ifdef USE_VisualCppExpress2008
  File "${SRC_ROOT}\windows\VisualCppExpress2008\Release\engrid.exe"
  File "${SRC_ROOT}\windows\VisualCppExpress2008\Release\libengrid.dll"
  File "${SRC_ROOT}\windows\VisualCppExpress2008\Release\nglib.dll"
!endif
  File "${SRC_ROOT}\licence.txt"
  File "${SRC_ROOT}\license.txt"
  File "${SRC_ROOT}\licence_exe.txt"

  ;Install libraries
  !include install_libraries.nsh
  ;Install files
  !include install_files.nsh

  ;Create links
  SetShellVarContext all
  CreateDirectory "$SMPROGRAMS\$ICONS_GROUP"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\enGrid.lnk" "$INSTDIR\engrid.exe" " " "$INSTDIR\engrid.exe"

SectionEnd

Section -AdditionalIcons
  SetShellVarContext all
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\enGits Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"

  WriteIniStr "$INSTDIR\${PRODUCT_NAME}_wiki.url" "InternetShortcut" "URL" "${WIKI_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\enGrid Wiki.lnk" "$INSTDIR\${PRODUCT_NAME}_wiki.url"

  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Browse Tutorials.lnk" "$INSTDIR\tutorials"

  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Browse Scripts for Blender.lnk" "$INSTDIR\blender_scripts"

  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk" "$INSTDIR\uninst.exe"
  ${RefreshShellIcons}
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\engrid.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\engrid.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"  "DesktopIcon" "$DESKTOP_ICON"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"  "ProgramGroup" "$ICONS_GROUP"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "InstallDir" "$INSTDIR"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "Version" "${PRODUCT_VERSION}"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "tmp_directory" "$TEMP"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "working_directory" "$DOCUMENTS"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  UAC::Unload
  Delete "$INSTDIR\UAC.dll"
  MessageBox MB_ICONINFORMATION|MB_OK "enGrid was successfully removed from your computer."
FunctionEnd

Function un.OnUnInstFailed
    UAC::Unload
FunctionEnd

Function un.onInit

!ifdef WIN64
  SetRegView 64
  ${DisableX64FSRedirection}
!endif

UAC_Elevate:
    UAC::RunElevated
    StrCmp 1223 $0 UAC_ElevationAborted
    StrCmp 0 $0 0 UAC_Err
    StrCmp 1 $1 0 UAC_Success
    Quit

UAC_Err:
    MessageBox mb_iconstop "Unable to elevate, error $0"
    Abort

UAC_ElevationAborted:
    MessageBox mb_iconstop "This installer requires admin access, aborting!"
    Abort

UAC_Success:
    StrCmp 1 $3 +4
    StrCmp 3 $1 0 UAC_ElevationAborted
    MessageBox mb_iconstop "This installer requires admin access, try again"
    goto UAC_Elevate

  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove enGrid and all of its components?" IDYES +2
  Abort

FunctionEnd


Section Uninstall
  SetShellVarContext all

  ;Remove files
  !include uninstall_files.nsh
  ;Remove libraries
  !include uninstall_libraries.nsh

  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\${PRODUCT_NAME}_wiki.url"
  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\engrid.exe"
  Delete "$INSTDIR\libengrid.dll"
  Delete "$INSTDIR\nglib.dll"
  Delete "$INSTDIR\license.txt"
  Delete "$INSTDIR\licence.txt"
  Delete "$INSTDIR\licence_exe.txt"

  ReadRegStr $DESKTOP_ICON ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DesktopIcon"
  StrCmp $R0 "0" +2
    Delete "$DESKTOP\enGrid.lnk"

  ReadRegStr $ICONS_GROUP ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "ProgramGroup"

  Delete "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\enGits Website.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\enGrid.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\enGrid Wiki.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Browse Tutorials.lnk"
  RMDir "$SMPROGRAMS\$ICONS_GROUP"

  ; Take care of the parent folder here as well
  ${GetParent} "$ICONS_GROUP" $R0
  StrCmp $R0 "enGits" 0 +3
    IfFileExists "$SMPROGRAMS\enGits" 0 +2
      RMDir "$SMPROGRAMS\enGits"

  RMDir "$INSTDIR"

  ; this is a "just in case" for Program Files only. It will not take care of other custom folders!
  ${GetParent} "$INSTDIR" $R0

!ifndef WIN64
  StrCmp $R0 "$PROGRAMFILES\enGits" 0 +3
    IfFileExists "$PROGRAMFILES\enGits" 0 +2
      RMDir "$PROGRAMFILES\enGits"
  StrCmp $R0 "$PROGRAMFILES32\enGits" 0 +3
    IfFileExists "$PROGRAMFILES32\enGits" 0 +2
      RMDir "$PROGRAMFILES32\enGits"
!else
  StrCmp $R0 "$PROGRAMFILES64\enGits" 0 +3
    IfFileExists "$PROGRAMFILES64\enGits" 0 +2
      RMDir "$PROGRAMFILES64\enGits"
!endif

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
SectionEnd
